!::
! New Game (Shell)
!::
constant GAME_TITLE	"GAME TITLE"

!:: Flags
!#set BETA                ! Compile with the comments-in-transcripts library
                          !   addition (requires "beta.h")
!#set CHEAP
!#set HUGOFIX             ! Compile with HugoFix Debugging Library commands
                          !   available (type $? in game)
!#set PRECOMPILED_LIBRARY ! Use a precompiled header library (see manual)
!#set VERBSTUBS           ! Include the grammar and routines from verbstubs.g
                          !   and verbstubs.h, respectively
!#set USE_ATTACHABLES     ! Use attachable items
!#set USE_PLURAL_OBJECTS  ! Use plural objects
#set USE_ROODYLIB
!#set USE_VEHICLES        ! Use vehicle objects
!#set VERSIONS            ! Print library versions at compile time

!:: switches              ! example how one can add compiler switches to source
!#switches -ls            ! print compilation stats to .LST file

#ifset HUGOFIX
#set DEBUG
#endif

!:: Other pre-Hugolib.h Constants
! (some constants must be set before hugolib is included)

!constant AFTER_PERIOD " "     ! include one space after full stops (as opposed
                               !   to two)
!constant INDENT_SIZE 0        ! no indentation in room descriptions and room
                               !   content listings

!:: Grammar Library Inclusions (grammar must come first)
#ifset USE_ROODYLIB
#include "roodylib.g"
#endif

xverb			"topics"
*										DoAskTellTopics

! new grammar needs to be defined before the including verblib
#include "verblib.g"        ! Verb Library

#include "hugolib.h"        ! Standard  Hugo Library
#ifset USE_ROODYLIB
#include "roodylib.h"         ! Hugo  Library Updates
#endif

#include "timesasked2.h"

!::	Other Library Inclusions
#ifset BETA
#include "beta.h"
#endif

#ifset CHEAP
#include "cheaplib.h"
#endif

!:: Other constants and global variables
!constant IFID "put-IFID here"

!::	Game Initialization	routine
routine init
{
!: First Things First
	SetGlobalsAndFillArrays
!: Screen clear section
#ifclear _ROODYLIB_H
	CenterTitle("Hugo Interactive Fiction")
	cls
	SetCharNumbers
#else
	SimpleIntro
	InitScreen
!: Set up any special libraries
	Init_Calls
#endif
!: Game opening
	IntroText
	MovePlayer(location)
}

routine SetGlobalsAndFillArrays
{
!\ Uncomment and modify this section if your game has scoring and ranking.
	MAX_SCORE = 50
	ranking[0] = "Amateur Adventurer"
	ranking[1] = "Competent Door-Unlocker"
	ranking[2] = "Bomb-Meddling Adventurer"
	ranking[3] = "Master Magic Wand Finder"
	ranking[4] = "The Genuine Article Sample Game Solver"
	MAX_RANK = 4  \!
	verbosity = 2
	counter = -1                    ! 1 turn before turn 0
	!ask_color = 0
	STATUSTYPE = 1   ! 1 = score/turns, 2 = time, 3 = moves: score:
	TEXTCOLOR = DEF_FOREGROUND
	BGCOLOR = DEF_BACKGROUND
	SL_TEXTCOLOR = DEF_SL_FOREGROUND
	SL_BGCOLOR = DEF_SL_BACKGROUND
	prompt = ">"
	DEFAULT_FONT = PROP_ON
	player = you
	location = STARTLOCATION
}

!::	Main game loop
routine	main
{
	counter	= counter +	1
	run	location.each_turn
	runevents
	RunScripts
	if parent(speaking) ~= location
		speaking = 0
	PrintStatusLine
#ifset _ROODYLIB_H
	Main_Calls
#endif
}

routine IntroText
{

}

player_character you "you"
{
	inherits topics ! we can ask about ourselves
}

room STARTLOCATION "Start Location"
{
	noun "room"
	inherits topics
}


character fred "Fred"
{
	noun "fred"
	in STARTLOCATION
	inherits chartopics
!	charnumber 1
	after
	{
		object DoAsk
		{
			local notcount
			select xobject
				case you
				{
					if not Asked
						"You seem swell."
					else
						"I said you seem swell."
				}
				case STARTLOCATION
				{
					select Asked
					case 0 : "I like swell places."
					case 1: "This place seems swell."
					case 2: "Sure, this place seems really swell."
					case else: "I'm done talking about how swell this
					place is."
				}
				case fred: "I'm just this guy."
				case else: "I dunno!"
			if not notcount
				xobject.timesasked #(self.charnumber) =  Asked + 1
		}

		object DoTell
		{
			select xobject
				case you
				{
					if not Told
						"You seem swell."
					else
						"I said you seem swell."
				}
				case STARTLOCATION
				{
					select Told
					case 0 : "I like swell places."
					case 1: "This place seems swell."
					case 2: "Sure, this place seems really swell."
					case else: "I'm done talking about how swell this
					place is."
				}
				case self: "I'm just this guy."
				case else: "I dunno!"
			xobject.timestold #(self.charnumber) =  Told + 1
		}

	}
}

replace CharRules(obj)  ! called by DoAskTellTopics
{
	local ret, b
	if verbroutine = &DoAsk
		b = Asked(obj)
	else
		b = Told(obj)
	select speaking
		case fred
		{
			select verbroutine
				case &DoAsk
				{
					select obj
						case you: ret = b < 1
						case STARTLOCATION: ret = b < 3
						case fred: ret = 1
						case else : ret = 0
				}
				case &DoTell
				{
					select obj
						case you: ret = b < 1
						case STARTLOCATION: ret = b < 3
						case fred: ret = 1
						case else : ret = 0
				}
	}
	return ret
}
